fn_main {
  intr_en(zero)
  a = label_fn_trap_handler
  intr_trap(a)
  fn_set_rand_seed(4000)
  fn_vt100_mode()
  fn_init_game()
  fn_print_board()
  a = 1
  intr_en(a)
  halt()
}

fn_vt100_mode {
  fn_putchar(27)
  fn_putchar(91)
  fn_putchar(50)
  fn_putchar(48)
  fn_putchar(104)
}

fn_init_game {
  push(a,b,i)
  for (i=0; i<16; i=i+1) {
    mem[i] = zero
  }
  fn_add_new()
  pop(i,b,a)
}

fn_set_rand_seed {
  push(a)
  a = 16
  mem[a] = rv
  pop(a)
}

fn_trap_handler {
  push(a,b,c)
  a = keyboard()
  b = 1
  intr_ack(b)

  c = 0
  if (a == 107) {
    c = fn_move_up()
  }
  if (a == 106) {
    c = fn_move_down()
  }
  if (a == 104) {
    c = fn_move_left()
  }
  if (a == 108) {
    c = fn_move_right()
  }

  if (c != 0) {
    fn_add_new()
  }
  
  fn_print_board()

  pop(c,b,a)
  iret()
}

fn_putchar {
  push(a)
  a = 1
  for (;a == 1;) {
    a = monitor_busy()
  }
  monitor(rv)
  pop(a)
}

fn_print_board {
  push(a,b,c,d,e,i)
  fn_print_line()
  
  for (i=0; i<4; i=i+1) {
    fn_putchar(124)

    b = i << 2
    for (c=0; c<4; c=c+1) {
      fn_putchar(32)

      d = b + c
      e = mem[d]

      if (e == 0) {
        fn_putchar(32)
      }
      if (e != 0) {
        a = e + 48
        fn_putchar(a)
      }

      fn_putchar(124)
    }
    fn_putchar(10)
    fn_print_line()
  }
  fn_putchar(10)
  pop(i,e,d,c,b,a)
}

fn_print_line {
  push(a,i)
  for (i=0; i<13; i=i+1) {
    fn_putchar(45)
  }
  fn_putchar(10)
  pop(i,a)
}

fn_add_new {
  push(a,b,c,d,e,f)
  f = 1
  a = fn_rand()
  for (b=0; b<16; b=b+1) {
    c = a + b
    d = 15
    d = c & d
    e = mem[d]
    if (e == 0) {
      mem[d] = f
      f = 0
    }
  }
  pop(f,e,d,c,b,a)
}

fn_rand {
  push(a,b,c)

  c = 16
  a = mem[c]
  b = a << 13
  a = a ^ b
  b = a >> 17
  a = a ^ b
  b = a << 5
  a = a ^ b
  mem[c] = a

  rv = a
  pop(c,b,a)
}

fn_compress {
  push(a,b,c,d,e,f,h)
  rv = 0

  for (a=0; a<4; a=a+1) {
    b = a << 2
    h = 0
    for (c=0; c<4; c=c+1) {
      d = b + c
      e = mem[d]
      if (e != 0) {
        f = b + h
        mem[f] = e
        if (h != c) {
          mem[d] = zero
          rv = 1
        }
        h = h + 1
      }
    }
  }
  pop(h,f,e,d,c,b,a)
}

fn_merge {
  push(a,b,c,d,e,f,g)
  rv = 0

  for (a=0; a<4; a=a+1) {
    b = a << 2
    for (c=0; c<3; c=c+1) {
      d = b + c
      e = mem[d]
      if (e != 0) {
        f = d + 1
        g = mem[f]
        if (e == g) {
          e = e + 1
          mem[d] = e
          mem[f] = zero
          rv = 1
        }
      }
    }
  }
  pop(g,f,e,d,c,b,a)
}

fn_reverse {
  push(a,b,c,d,e,f,g)
  for (a=0; a<4; a=a+1) {
    b = a << 2
    for (c=0; c<2; c=c+1) {
      d = b + c
      e = mem[d]
      f = b - c
      f = f + 3
      g = mem[f]
      mem[d] = g
      mem[f] = e
    }
  }
  pop(g,f,e,d,c,b,a)
}

fn_transpose {
  push(a,b,c,d,e,f,g)
  for (a=0; a<4; a=a+1) {
    b = a << 2
    for (c=0; c<4; c=c+1) {
      if (a < c) {
        d = b + c
        e = mem[d]
        f = c << 2
        f = f + a
        g = mem[f]
        mem[d] = g
        mem[f] = e
      }
    }
  }
  pop(g,f,e,d,c,b,a)
}

fn_move_left {
  push(a,b,c)
  a = fn_compress()
  b = fn_merge()
  c = fn_compress()
  a = a | b
  a = a | c
  rv = a
  pop(c,b,a)
}

fn_move_right {
  push(a)
  fn_reverse()
  a = fn_move_left()
  fn_reverse()
  rv = a
  pop(a)
}

fn_move_up {
  push(a)
  fn_transpose()
  a = fn_move_left()
  fn_transpose()
  rv = a
  pop(a)
}

fn_move_down {
  push(a)
  fn_transpose()
  a = fn_move_right()
  fn_transpose()
  rv = a
  pop(a)
}
